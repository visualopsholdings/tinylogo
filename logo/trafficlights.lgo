#   rgb.lgo
#
#   Author: Paul Hamilton (paul@visualops.com)
#   Date: 2-Jun-2023
#
#   Allow a button to change traffic lights.
#
#   This work is licensed under the Creative Commons Attribution 4.0 International License. 
#   To view a copy of this license, visit http://creativecommons.org/licenses/by/4.0/ or 
#   send a letter to Creative Commons, PO Box 1866, Mountain View, CA 94042, USA.
#
#   https://github.com/visualopsholdings/tinylogo  
#

# send a raw analog value to an RGB LED's redm green and blue channel (0 is full on)
BUILTIN(REDR:1,GREENR:1,BLUER:1,BUTTON1,BUTTON2)

# scale a color to be between 1 and 100
TO SUBC :C
  100 - :C
END
TO DIVC :C
  SUBC :C / 100
END
TO SCLR :C
  DIVC :C * 255
END

TO REDC :N
  REDR SCLR :N
END

TO GREENC :N
  GREENR SCLR :N
END

TO BLUEC :N
  BLUER SCLR :N
END

# arguments are reversed!
TO SETALL :B :G :R
  REDC :R
  GREENC :G
  BLUEC :B
END

TO AMBER
 SETALL 100 75 0
END

TO RED
  SETALL 100 0 0
END

TO GREEN
  SETALL 0 100 0
END

TO BLUE
  SETALL 0 0 100
END

TO OFF
  SETALL 0 0 0
END

TO NEXTSTATE1
  :STATE1 + 1
END

TO FIRSTSTATE1
  MAKE "STATE1 0
END

TO NOTRED
  IFELSE :STATE1 = 1 GREEN AMBER
END

TO RESET
  MAKE "STATE1 FIRSTSTATE1
  RED
END

TO PRESS1
  MAKE "STATE1 NEXTSTATE1
  IF :STATE1 > 2 FIRSTSTATE1
  IFELSE :STATE1 = 0 RED NOTRED
END

TO TESTST1
  MAKE "OLDVALUE1 :VALUE1
  IF :VALUE1 = 1 PRESS1
END

TO TESTB1
  MAKE "VALUE1 BUTTON1
  IF :VALUE1 != :OLDVALUE1 TESTST1
END

TO PRESS2
  RESET
END

TO TESTST2
  MAKE "OLDVALUE2 :VALUE2
  IF :VALUE2 = 1 PRESS2
END

TO TESTB2
  MAKE "VALUE2 BUTTON2
  IF :VALUE2 != :OLDVALUE2 TESTST2
END

TO TESTB
  TESTB1
  TESTB2
END

RED
FOREVER TESTB
 
