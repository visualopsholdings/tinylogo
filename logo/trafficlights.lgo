#   rgb.lgo
#
#   Author: Paul Hamilton (paul@visualops.com)
#   Date: 2-Jun-2023
#
#   Allow a button to change traffic lights.
#
#   This work is licensed under the Creative Commons Attribution 4.0 International License. 
#   To view a copy of this license, visit http://creativecommons.org/licenses/by/4.0/ or 
#   send a letter to Creative Commons, PO Box 1866, Mountain View, CA 94042, USA.
#
#   https://github.com/visualopsholdings/tinylogo  
#

# send a raw analog value to an RGB LED's redm green and blue channel (0 is full on)
BUILTIN(RED1R:1,GREEN1R:1,BLUE1R:1,RED2R:1,GREEN2R:1,BLUE2R:1,BUTTON1,BUTTON2)

# scale a color to be between 1 and 100
TO SUBC :C
  100 - :C
END
TO DIVC :C
  SUBC :C / 100
END
TO SCLR :C
  DIVC :C * 255
END

# arguments are reversed!
TO SET1 :B :G :R
  RED1R SCLR :R
  GREEN1R SCLR :G
  BLUE1R SCLR :B
END

# arguments are reversed!
TO SET2 :B :G :R
  RED2R SCLR :R
  GREEN2R SCLR :G
  BLUE2R SCLR :B
END

# the named colors we use for each LED

TO AMBER1
 SET1 100 75 0
END

TO RED1
  SET1 100 0 0
END

TO GREEN1
  SET1 0 100 0
END

TO RED2
  SET2 8 2 2
END

TO GREEN2
  SET2 2 8 2
END

TO OFF1
  SET1 0 0 0
END

# the button logic

# STATE1 holds 0 for RED, 1 for GREEN and 2 for AMBER for the first LED
# STATE2 holds 0 for OFF and 1 for ON

TO NEXTSTATE1
  :STATE1 + 1
END

TO FIRSTSTATE1
  MAKE "STATE1 0
END

TO NOTRED1
  IFELSE :STATE1 = 1 GREEN1 AMBER1
END

TO PRESS1
  MAKE "STATE1 NEXTSTATE1
  IF :STATE1 > 2 FIRSTSTATE1
  IFELSE :STATE1 = 0 RED1 NOTRED1
END

TO TESTST1
  MAKE "OLDVALUE1 :VALUE1
  IF :VALUE1 = 1 PRESS1
END

TO TESTB1
  MAKE "VALUE1 BUTTON1
  IF :VALUE1 != :OLDVALUE1 TESTST1
END

TO OFF
  OFF1
  RED2
END

TO ON
  RED1
  GREEN2
END

TO NOTSTATE2
  NOT :STATE2
END

TO PRESS2
  MAKE "STATE2 NOTSTATE2
  IFELSE :STATE2 = 0 OFF ON
END

TO TESTST2
  MAKE "OLDVALUE2 :VALUE2
  IF :VALUE2 = 1 PRESS2
END

TO TESTB2
  MAKE "VALUE2 BUTTON2
  IF :VALUE2 != :OLDVALUE2 TESTST2
END

TO TESTB
  IF :STATE2 = 1 TESTB1
  TESTB2
END

TO START
  MAKE "STATE2 1
  GREEN2
  RED1
  FOREVER TESTB
END

START


 
