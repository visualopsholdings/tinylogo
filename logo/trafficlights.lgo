#   trafficlights.lgo
#
#   Author: Paul Hamilton (paul@visualops.com)
#   Date: 2-Jun-2023
#
#   Allow a button to change traffic lights. The second button turns
#   the entire thing on and off and changes a second LED to light red
#   when it's off and light green when it's on.
#
#   This work is licensed under the Creative Commons Attribution 4.0 International License. 
#   To view a copy of this license, visit http://creativecommons.org/licenses/by/4.0/ or 
#   send a letter to Creative Commons, PO Box 1866, Mountain View, CA 94042, USA.
#
#   https://github.com/visualopsholdings/tinylogo  
#

# change your pins here
to B1PIN
  2
end

to R1PIN
  3
end

to G1PIN
  4
end

to BTN2PIN
  6
end

to BTN1PIN
  7
end

to B2PIN
  9
end

to R2PIN
  10
end

to G2PIN
  11
end

# setup all the pins for in out and values
to SETUP
  pinout R1PIN
  aout R1PIN 255
  pinout G1PIN
  aout G1PIN 255
  pinout B1PIN
  aout B1PIN 255
  
  pinout R2PIN
  aout R2PIN 255
  pinout G2PIN
  aout G2PIN 255
  pinout B2PIN
  aout B2PIN 255

  pinin BTN1PIN
  pinin BTN2PIN
end

# send raw values out to each pin
to RED1R :V
  aout R1PIN :V
end

to GREEN1R :V
  aout G1PIN :V
end

to BLUE1R :V
  aout B1PIN :V
end

to RED2R :V
  aout R2PIN :V
end

to GREEN2R :V
  aout G2PIN :V
end

to BLUE2R :V
  aout B2PIN :V
end

to BUTTON1
  DREAD BTN1PIN
end

to BUTTON2
  DREAD BTN2PIN
end

# scale a color to be between 1 and 100
to SCLR :C
  ((100 - :C) / 100) * 255
end

# arguments are reversed!
to SET1 :B :G :R
  RED1R SCLR :R
  GREEN1R SCLR :G
  BLUE1R SCLR :B
end

# arguments are reversed!
to SET2 :B :G :R
  RED2R SCLR :R
  GREEN2R SCLR :G
  BLUE2R SCLR :B
end

# the named colors we use for each LED

to AMBER1
 SET1 100 75 0
end

to RED1
  SET1 100 0 0
end

to GREEN1
  SET1 0 100 0
end

to RED2
  SET2 8 2 2
end

to GREEN2
  SET2 2 8 2
end

to OFF1
  SET1 0 0 0
end

# the button logic

# STATE1 holds 0 for RED, 1 for GREEN and 2 for AMBER for the first LED
# STATE2 holds 0 for OFF and 1 for ON

to FIRSTSTATE1
  make "STATE1 0
end

to NOTRED1
  ifelse :STATE1 = 1 GREEN1 AMBER1
end

to PRESS1
  make "STATE1 :STATE1+1
  if :STATE1 > 2 FIRSTSTATE1
  ifelse :STATE1 = 0 RED1 NOTRED1
end

to TESTST1
  make "OLDVALUE1 :VALUE1
  if :VALUE1 = 1 PRESS1
end

to TESTB1
  make "VALUE1 BUTTON1
  if :VALUE1 != :OLDVALUE1 TESTST1
end

to OFF
  OFF1
  RED2
end

to ON
  RED1
  GREEN2
end

to PRESS2
  make "STATE2 !:STATE2
  ifelse :STATE2 = 0 OFF ON
end

to TESTST2
  make "OLDVALUE2 :VALUE2
  if :VALUE2 = 1 PRESS2
end

to TESTB2
  make "VALUE2 BUTTON2
  if :VALUE2 != :OLDVALUE2 TESTST2
end

to TESTB
  if :STATE2 = 1 TESTB1
  TESTB2
end

to START
  make "STATE2 1
  GREEN2
  RED1
  FOREVER TESTB
end

START


 
