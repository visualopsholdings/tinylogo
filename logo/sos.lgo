#   sos.lgo
#
#   Author: Paul Hamilton (paul@visualops.com)
#   Date: 2-Jun-2023
#
#   Signal for help when a button is pressed.
#
#   This work is licensed under the Creative Commons Attribution 4.0 International License. 
#   To view a copy of this license, visit http://creativecommons.org/licenses/by/4.0/ or 
#   send a letter to Creative Commons, PO Box 1866, Mountain View, CA 94042, USA.
#
#   https://github.com/visualopsholdings/tinylogo  
#

to ID
  PRINT "sos
end

# change your pins here
to LEDPIN
  13
end

to BTNPIN
  9
end

to SETUP
  pinout LEDPIN
  dlow LEDPIN
  pininup BTNPIN
end

to ON
  dhigh LEDPIN
end

to OFF
  dlow LEDPIN
end

to DOT
  ON wait 300 OFF wait 300
end

to DASH
  ON wait 800 OFF wait 100
end

to SPACE
  wait 200
end

to S
  DOT DOT DOT
  SPACE
end

to O
  DASH DASH DASH
  SPACE
end

to HELP
  print "HELP
  S O S
  print "OK
end

# handling the button

to BUTTON
  dread BTNPIN
end

to TESTSTATE
  make "OLDVALUE :VALUE
  if :VALUE = 0 HELP
end

to TESTBTN
  make "VALUE BUTTON
  if :VALUE != :OLDVALUE TESTSTATE 
end

to RUN
  forever TESTBTN
end

RUN
